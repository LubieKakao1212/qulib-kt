buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        // Make sure this version matches the one included in Kotlin for Forge
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.7.21'
        // OPTIONAL Gradle plugin for Kotlin Serialization
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.7.21'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'maven-publish'

ext.kotlin_version = '1.7.21'
ext.coroutines_version = '1.6.4'
ext.serialization_version = '1.4.1'

project.properties['kotlin.stdlib.default.dependency'] = false

//apply from: 'https://raw.githubusercontent.com/thedarkcolour/KotlinForForge/site/thedarkcolour/kotlinforforge/gradle/kff-3.8.0.gradle'

version = '1.0.24'
group = 'com.LubieKakao1212.qulib'
archivesBaseName = 'qulib-kt'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

configurations {
    library
    shade
    implementation.extendsFrom shade
}

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

minecraft {
    // Change to your preferred mappings
    mappings channel: 'parchment', version: '2023.03.12-1.19.3'
    // Add your AccessTransformer
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,LOADING,CORE'
            property 'forge.logging.console.level', 'debug'

            //jvmArgs '--add-modules', '', '--add-reads', 'kotlin.stdlib=kotlinx.coroutines.core.jvm'

            mods {
                qulib {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'SCAN,LOADING,CORE'
            property 'forge.logging.console.level', 'debug'

            mods {
                qulib {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,LOADING,CORE'
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'qulib', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources')

            mods {
                qulib {
                    source sourceSets.main
                }
            }
        }
    }
}

// Include assets and data from data generators
sourceSets.main.resources { srcDirs 'src/generated/resources/' }

repositories {
    maven {
        name = 'Kotlin for Forge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }
}

dependencies {
    // Use the latest version of Minecraft Forge
    minecraft 'net.minecraftforge:forge:1.19.3-44.1.0'

    implementation 'thedarkcolour:kotlinforforge:3.8.0'

    minecraftLibrary "org.joml:joml:${jomlVersion}"

    shade "org.joml:joml:${jomlVersion}"

    groovy.lang.Closure exclude_annotations = { exclude group: 'org.jetbrains', module: 'annotations' }

    library(group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: kotlin_version, exclude_annotations)
    library(group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: kotlin_version, exclude_annotations)
    library(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: coroutines_version, exclude_annotations)
    library(group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-jdk8', version: coroutines_version, exclude_annotations)
    library(group: 'org.jetbrains.kotlinx', name: 'kotlinx-serialization-json', version: serialization_version, exclude_annotations)
}

compileKotlin {
    kotlinOptions.jvmTarget = "17"
}

jar {
    manifest {
        attributes([
                'Specification-Title': "qulib",
                'Specification-Vendor': "examplemodsareus",
                'Specification-Version': "1", // We are version 1 of ourselves
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Implementation-Vendor' :"examplemodsareus",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

// Example configuration to allow publishing using the maven-publish plugin
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar')
jar.finalizedBy('sourcesJar')
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
// publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
            artifact sourcesJar

            groupId = 'com.LubieKakao1212.qulib-kt'
        }
    }
    repositories {
        maven {
            url "file://${publishDir}/mcmodsrepo"
        }
    }
}


shadowJar {
    classifier = ""
    configurations = [project.configurations.shade]
    relocate 'org.joml', "${project.group}.libs.joml"
    //relocate 'com.fathzer', "${project.group}.libs.javaluator"
    finalizedBy 'reobfShadowJar'
}

tasks.build.dependsOn shadowJar
artifacts {
    archives shadowJar
}

reobf {
    shadowJar {
    }
}
tasks.reobfShadowJar.mustRunAfter shadowJar

tasks.build.dependsOn reobfShadowJar