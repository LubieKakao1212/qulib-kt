plugins {
    id "org.jetbrains.kotlin.jvm" version "1.9.22"
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.3-SNAPSHOT" apply false
}

//Load Access Properties (ghReadPAT, ghUsr)
ext.accessProps = new Properties()
var propsFile = file("access.properties")
if(propsFile.exists()) {
    propsFile.withInputStream {
        accessProps.load(it)
    }
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    base {
        archivesName = "$rootProject.archives_base_name-${project.loader}-$minecraft_version"
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the yarn mappings you may select this one as well.
        mappings "net.fabricmc:yarn:1.19.4+build.2:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "kotlin"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    dependencies {
        compileOnly("org.jetbrains.kotlin:kotlin-stdlib")
    }

    kotlin.target.compilations.all {
        kotlinOptions.jvmTarget = "17"
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}